quick := ""
suites := "full"
_flags := if quick != "" { "-- --quick" } else { "" }

[private]
_list_raw:
    @cargo bench --bench common --features {{suites}} --manifest-path ../Cargo.toml -- __no_match__ 2>/dev/null

# Print names of all enabled suites.
list:
    @echo "Available suites:"
    @just suites={{suites}} _list_raw | grep suite_name | awk '{print "  " $2}'

# Print detailed info for all enabled suites.
list-ext:
    @just suites={{suites}} _list_raw

[private]
_bench name:
    cargo bench --bench {{name}} --features {{suites}},ring --manifest-path ../Cargo.toml {{_flags}}

common: (_bench "common")

ietf: (_bench "ietf")

pedersen: (_bench "pedersen")

ring: (_bench "ring")

all: common ietf pedersen ring
